      //*********************************************************************************************                   
      // Program:    WRT2IFS                                                                                            
      // Purpose:    Write single byte character textes into the IFS                                                    
      // Parameters: ParText     => Text to be written into the IFS                                                     
      //             ParIFSFile  => Qualified IFS File name (e.g. /home/Hauser/MyIFSFile.txt')                          
      //             ParFileOpt  => File Operation: 8 = Create a new file, return an error if already exists            
      //                                           16 = Create a new file, override if already exists                   
      //                                           32 = Create a new file, append at the end if already exists          
      //                            Optional: Not Passed = File Operation 8 = SQFCRT                                    
      // Programmer: B.Hauser                                                                                           
      // Creation:   2017-01-20                                                                                         
      //*********************************************************************************************                   
      // H E A D E R    S P E C I F I C A T I O N S                                                                     
      //*********************************************************************************************                   
        Ctl-Opt DatFmt(*ISO) BNDDIR('HSBNDDIR')                                                                         
                /If Defined (*CRTBNDRPG)                                                                                
                    ActGrp('WRT2IFS')                                                                                   
                /EndIf                                                                                                  
                Main(Wrt2IFS);                                                                                          
      //*********************************************************************************************                   
      // P R O T O T Y P E S                                                                                            
      //*********************************************************************************************                   
      /Include QPROLESRC,SNDMSG                                                                                         
      //*********************************************************************************************                   
      // G L O B A L   V A R I A B L E S   A N D   D A T A   S T R U C T U R E S                                        
      //*********************************************************************************************                   
        DCL-DS PGMSDS   PSDS       Qualified;                                                                           
          MsgTxt        Char(80)   Pos(91);                                                                             
        End-Ds;                                                                                                         
      //*********************************************************************************************                   
      // S Q L   O P T I O N S                                                                                          
      //*********************************************************************************************                   
       Exec SQL   Set Option  Commit=*None, DatFmt=*ISO, TimFmt=*ISO,                                                   
                              Naming=*SYS,  CloSQLCsr=*EndActGrp;                                                       
      //*********************************************************************************************                   
      // M A I N   P R O G R A M                                                                                        
      //*********************************************************************************************                   
      // Main Procedure: WRT2IFS                                                                                        
      //*********************************************************************************************                   
       DCL-Proc Wrt2IFS;                                                                                                
                                                                                                                        
         DCL-PI Wrt2IFS;                                                                                                
           ParText         VarChar(16000000)  Const;                           //Text to be written into the IFS        
           ParIFSFile      VarChar(1024)      Const;                           //IFS File Name                          
           PInOper         Int(5)             Const  Options(*NoPass);         //File Operation                         
         End-Pi;                                                                                                        
                                                                                                                        
         DCL-S  ParOper         Int(5);                                        //File Operation                         
                                                                                                                        
         DCL-S  LocErrMsg       Char(132);                                     //Error Message                          
         DCL-S  LocClobFile     SQLTYPE(Clob_File);                            //IFS Reference File                     
         DCL-S  LocClobText     SQLTYPE(Clob: 16000000);                       //Text                                   
         //--------------------------------------------------------------------------------------------                 
         Monitor;                                                                                                       
            // Check in incoming paramters                                                                              
            If %Len(%Trim(ParText)) = *Zeros;                                                                           
               SndEscMsgLinMain('No Text passed');                                                                      
            EndIf;                                                                                                      
                                                                                                                        
            If %Len(%Trim(ParIFSFile)) = *Zeros;                                                                        
               SndEscMsgLinMain('No IFS File specified');                                                               
            EndIf;                                                                                                      
                                                                                                                        
            If %Parms >= %ParmNum(PInOper);                                    //With Operation                         
               If PInOper = SQFCRT or PInOper = SQFOVR or PInOper = SQFAPP;                                             
                 //Check valid File Operation                                                                           
                  ParOper = PInOper;                                           //Valid Operation                        
               ElseIf PInOper <> *Zeros;                                                                                
                  SndEscMsgLinMain('Invalid File Operation');                  //Invalid Operation                      
               EndIf;                                                                                                   
            Else;                                                                                                       
               ParOper = SQFCRT;                                               //Default = Create                       
            EndIf;                                                                                                      
                                                                                                                        
            //Initialize File Information                                                                               
            Clear LocClobFile;                                                                                          
            LocClobFile_Name = %Trim(ParIFSFile);                              //IFS file Name                          
            LocClobFile_NL   = %Len(%Trim(LocClobFile_Name));                  //IFS file Name Length                   
            LocClobFile_FO   = ParOper;                                        //File Operation                         
                                                                                                                        
            LocClobText_Data = ParText;                                        //Text                                   
            LocClobText_Len  = %Len(ParText);                                  //Text Length                            
                                                                                                                        
            Exec SQL  Set :LocClobFile = :LocClobText;                                                                  
            If SQLCODE < *Zeros;                                                                                        
               Exec SQL Get Diagnostics Condition 1 :LocErrMsg = MESSAGE_TEXT;                                          
               SndEscMsgLinMain(LocErrMsg);                                                                             
            EndIf;                                                                                                      
                                                                                                                        
         On-Error;                                                                                                      
            SndEscMsgLinMain(PGMSDS.MsgTxt);                                                                            
         EndMon;                                                                                                        
       End-Proc Wrt2IFS;                                                                                                
